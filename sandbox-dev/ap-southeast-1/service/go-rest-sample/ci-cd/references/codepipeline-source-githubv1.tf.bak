resource "aws_codebuild_project" "pipeline_build" {
  name          = "go-rest-sample-pipeline_cd"
  service_role  = aws_iam_role.main.arn
  build_timeout = 20

  artifacts {
    type = "CODEPIPELINE"
  }

  environment {
    compute_type                = "BUILD_GENERAL1_SMALL"
    image                       = "aws/codebuild/standard:7.0"
    image_pull_credentials_type = "CODEBUILD"
    type                        = "LINUX_CONTAINER"
  }

  logs_config {
    cloudwatch_logs {
      group_name = "codebuild"
    }
  }

  source {
    type      = "CODEPIPELINE"
    buildspec = file("buildspec.yml")
  }

  cache {
    type  = "LOCAL"
    modes = ["LOCAL_CUSTOM_CACHE", "LOCAL_SOURCE_CACHE"]
  }

  tags = {
    Test = "Value"
  }
}


resource "aws_codepipeline" "pipeline" {
  name     = "test-pipeline"
  role_arn = aws_iam_role.codepipeline.arn

  artifact_store {
    location = aws_s3_bucket.codepipeline.bucket
    type     = "S3"
  }

  stage {
    name = "Source"

    action {
      name             = "SourceAction"
      category         = "Source"
      owner            = "ThirdParty"
      provider         = "GitHub"
      version          = "1"
      output_artifacts = ["SourceOutputArtifact"]

      configuration = {
        Owner                = "andrewsaputra"
        Repo                 = "go-rest-sample"
        Branch               = "modify-appspec"
        OAuthToken           = "<github token>"
        PollForSourceChanges = false
      }
    }
  }

  stage {
    name = "Build"

    action {
      name             = "BuildAction"
      category         = "Build"
      owner            = "AWS"
      provider         = "CodeBuild"
      version          = "1"
      input_artifacts  = ["SourceOutputArtifact"]
      output_artifacts = ["BuildOutputArtifact"]

      configuration = {
        ProjectName = aws_codebuild_project.pipeline_build.name
      }
    }
  }

  stage {
    name = "Deploy"

    action {
      name            = "DeployAction"
      category        = "Deploy"
      owner           = "AWS"
      provider        = "CodeDeploy"
      version         = "1"
      input_artifacts = ["BuildOutputArtifact"]

      configuration = {
        ApplicationName     = aws_codedeploy_deployment_group.app.app_name
        DeploymentGroupName = aws_codedeploy_deployment_group.app.deployment_group_name
      }
    }
  }
}

resource "aws_codepipeline_webhook" "pipeline" {
  name            = "test-pipeline-webhook"
  authentication  = "GITHUB_HMAC"
  target_action   = "SourceAction"
  target_pipeline = aws_codepipeline.pipeline.name

  authentication_configuration {
    secret_token = "dummy-secret-token"
  }

  filter {
    json_path    = "$.ref"
    match_equals = "refs/heads/modify-appspec"
  }
}


provider "github" {
  token = "<github token>"
}

resource "github_repository_webhook" "pipeline" {
  repository = "go-rest-sample"

  configuration {
    url          = aws_codepipeline_webhook.pipeline.url
    content_type = "json"
    insecure_ssl = true
    secret       = "dummy-secret-token"
  }

  events = ["push"]
}
